buildscript {
    ext.kotlin_version = '1.3.61'
    ext {
        springBootVersion = '2.3.0.M2'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
    id "org.ajoberstar.grgit" version "4.0.1"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.61"
    id "org.jetbrains.kotlin.plugin.jpa" version '1.3.61'
}

noArg {
    invokeInitializers = true
}

// for spring boot devtools
configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

group 'net.ehicks'
version '1.0.0-' + grgit.head().abbreviatedId

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
    implementation 'de.codecentric:spring-boot-admin-starter-client:2.2.2'
    
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.data:spring-data-envers'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.hibernate:hibernate-envers:5.4.11.Final'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.+"
    implementation 'net.sargue:java-time-jsptags:1.1.4'
    implementation 'org.apache.tika:tika-core:1.23'
    implementation 'commons-fileupload:commons-fileupload:1.4'

    implementation group: "com.github.ehicks05", name: "common", version: "-SNAPSHOT", changing: true

    implementation 'org.apache.pdfbox:pdfbox:2.0.18'
    implementation 'com.github.dhorions:boxable:1.5'

    implementation 'org.imgscalr:imgscalr-lib:4.2'
    implementation 'com.sksamuel.diff:diff:1.1.11'
    implementation 'it.sauronsoftware.cron4j:cron4j:2.2.5'
    
    implementation 'javax.websocket:javax.websocket-api:1.1'

    implementation 'javax.servlet:jstl:1.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'

    implementation 'org.postgresql:postgresql:42.2.10'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

// Task to create a version file based on git repo data
import org.ajoberstar.grgit.*
import java.time.ZonedDateTime

task gitProperties()  {
    doLast {
        def separator = System.getProperty('line.separator')
        Commit head = grgit.head()

        def file = new File("$projectDir/src/main/resources/git.properties")
        file.setText("")
        file << "version= ${grgit.log().size()}$separator"
        file << "revision= ${head.id}$separator"
        file << "revisionDate= ${head.dateTime}$separator"
        file << "author= ${head.author.name} (${head.author.email})$separator"
        file << "buildDate= ${ZonedDateTime.now()}$separator"
    }
}

tasks.war.dependsOn gitProperties

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}